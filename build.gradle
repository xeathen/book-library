apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'book-library'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
            content {
                includeGroupByRegex 'core\\.framework.*'
            }
        }
    }
}

def coreNGVersion = '7.2.1'

configure(subprojects.findAll { it.name.endsWith('-db-migration') }) {
    apply from: file("${rootDir}/gradle/db-migration.gradle")

    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
    }
}

configure(subprojects.findAll { it.name.endsWith('-mongo-migration') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
    }
    tasks.register('mongoMigrate') {
        dependsOn run
    }
}

configure(subprojects.findAll { it.name.matches('.*-service-interface') }) {
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}

configure([project(':user-service'),
           project(':user-service-interface')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

configure([project(':book-service'),
           project(':book-service-interface')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

configure([project(':notification-service')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

configure([project(':back-office'),
           project(':back-office-interface')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

configure([project(':library-site'),
           project(':library-site-interface')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

project(':user-service') {
    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
        implementation project(':user-service-interface')
    }
}

project(':book-service') {
    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
        implementation project(':book-service-interface')
        implementation project(':user-service-interface')
    }
}

project(':notification-service') {
    dependencies {
        implementation project(':book-service-interface')
        implementation project(':user-service-interface')
    }
}



project(':library-site') {
    dependencies {
        implementation project(':library-site-interface')
        implementation project(':book-service-interface')
        implementation project(':user-service-interface')
    }
}